module c2test;

import stdio local;
import csetjmp local;

public type TestFunc func void();

public type Test struct {
    const char* name;
    TestFunc run;
}

const char[] ColorBlue   = "\033[0;34m";
const char[] ColorNormal = "\033[0m";
const char[] ColorRed    = "\033[0;31m";
const char[] ColorYellow = "\033[0;33m";

char[4096] error_buffer;
char* error_msg;
JmpBuf c2test_err;

u32 num_ok;
u32 num_failed;
u32 cur_test;

public func void runtests(const Test* tests, u32 num_tests)
{
    num_ok = 0;
    num_failed = 0;
    for (cur_test = 0; cur_test < num_tests; cur_test++) {
        u32 i = cur_test;
        printf("TEST %u/%u %s ", i+1, num_tests, tests[i].name);
        error_buffer[0] = 0;
        error_msg = error_buffer;
        i32 result = setjmp(&c2test_err);
        if (result == 0) {
            tests[i].run();
            printf("[OK]\n");
            num_ok++;
        } else {
            printf("%s[FAIL]%s\n", ColorRed, ColorNormal);
            num_failed++;
        }
        if (error_msg != error_buffer) {
            printf("%s", error_buffer);
        }
    }
    const char* color = ColorNormal;
    if (num_failed) color = ColorRed;
    printf("%sRESULTS: %d tests (%d ok, %d failed)%s\n", color,
        num_tests, num_ok, num_failed, ColorNormal);
}

func void color_print(const char* color, const char* title, const char* filename, u32 line, const char* msg) {
    u32 len;
    if (filename) {
        len = cast<u32>(sprintf(error_msg, "  %s%s: %s:%d %s%s\n", color, title, filename, line, msg, ColorNormal));
    } else {
        len = cast<u32>(sprintf(error_msg, "  %s%s: %s%s\n", color, title, msg, ColorNormal));
    }
    error_msg += len;
}

public func void fail(const char* filename, u32 line, char* msg = nil)
{
    if (msg == nil) msg = "failed";
    color_print(ColorYellow, "ERR", filename, line, msg);
    longjmp(c2test_err, 1);
}

public func void assert_equal(i64 exp, i64 real, const char* caller, u32 line)
{
    if (exp != real) {
        char[64] msg;
        sprintf(msg, "expected %ld, got %ld", exp, real);
        color_print(ColorYellow, "ERR", caller, line, msg);
    }
}

public func void log(const char* msg)
{
    color_print(ColorBlue, "LOG", nil, 0, msg);
}

